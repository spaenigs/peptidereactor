from modlamp.core import read_fasta
from PseKRAAC.type1 import type1

import pandas as pd
import numpy as np
import os

TOKEN = config["token"]
TARGET_FILES = config["csv_out"]

if type(TARGET_FILES) == list:
    TARGET_DIR = os.path.dirname(TARGET_FILES[0])
else:
    TARGET_DIR = os.path.dirname(TARGET_FILES)

rule all:
    input:
         config["csv_out"]

rule encode:
    input:
        config["fasta_in"]
    output:
        temp(f"data/temp/{TOKEN}/psekraac_type1_subtype-{{sub_val}}_raactype-{{raac_val}}_" + \
             f"ktuple-{{ktuple_val}}_lambda-{{lambda_val}}.csv")
    run:
        seqs, names = read_fasta(str(input[0]))
        fastas = [[n, s] for s, n in zip(seqs, names)]
        res = np.array(type1(fastas,
                             subtype=wildcards.sub_val,
                             raactype=int(wildcards.raac_val),
                             ktuple=int(wildcards.ktuple_val),
                             glValue=int(wildcards.lambda_val)))
        df = pd.DataFrame(res[1:, 1:], columns=res[0, 1:])
        df.index = np.array(res)[1:, 0]
        df.to_csv(str(output))

rule dump:
    input:
        f"data/temp/{TOKEN}/psekraac_type1_subtype-{{sub_val}}_raactype-{{raac_val}}_" + \
        f"ktuple-{{ktuple_val}}_lambda-{{lambda_val}}.csv",
        config["classes_in"]
    output:
        f"{TARGET_DIR}/psekraac_type1_subtype-{{sub_val}}_raactype-{{raac_val}}_" + \
        f"ktuple-{{ktuple_val}}_lambda-{{lambda_val}}.csv"
    run:
        with open(str(input[1])) as f:
            classes = list(map(lambda l: int(l.rstrip()), f.readlines()))
        df = pd.read_csv(str(input[0]), index_col=0)
        df["y"] = classes
        df.to_csv(str(output))
