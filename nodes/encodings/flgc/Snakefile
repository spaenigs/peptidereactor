import os
import pandas as pd

TOKEN = config["token"]
TARGET_FILES = config["csv_out"]
if type(TARGET_FILES) == list:
    TARGET_DIR = os.path.dirname(TARGET_FILES[0])
else:
    TARGET_DIR = os.path.dirname(TARGET_FILES)

def parse_aaindices(path):
    aaindex_df = pd.read_csv(path, sep="\t", index_col=0)\
                     .iloc[:, :-1]
    aaindex_df.columns = pd.read_csv(path, sep="\t", index_col=0)\
                             .columns.values[1:]
    return aaindex_df

rule all:
    input:
         config["csv_out"]

rule get_cluster:
    input:
         "peptidereactor/iFeature/data/AAindex.tsv"
    output:
         temp(f"data/temp/{TOKEN}/flgc/aaindex_cluster_{{aaindex}}.csv")
    run:
         from sklearn.cluster import KMeans

         aaindex_df = parse_aaindices(str(input))

         kmeans = KMeans(n_clusters=5)\
             .fit(aaindex_df.loc[wildcards.aaindex, :].values.reshape(-1, 1))

         sorted_df = pd.DataFrame({
             "aa": aaindex_df.columns,
             "val": aaindex_df.loc[wildcards.aaindex, :],
             "class": kmeans.labels_}).sort_values(by="val")
         sorted_df["new_class"] = "NA"

         new_classes = ["T", "H", "M", "L", "B"]
         for c in sorted_df["class"].unique():
             sorted_df.loc[sorted_df["class"] == c, "new_class"] = new_classes.pop()

         sorted_df.to_csv(str(output))

rule encode_five_level_grouping_composition:
    input:
         config["csv_in"],
         f"data/temp/{TOKEN}/flgc/aaindex_cluster_{{aaindex}}.csv"
    output:
         f"{TARGET_DIR}/flgc_{{aaindex}}.csv"
    run:
         df = pd.read_csv(str(input[0]), index_col=0)
         sorted_df = pd.read_csv(str(input[1]))

         df_res = df.copy()
         for idx in df.index:
             for group in ["T", "H", "M", "L", "B"]:
                 aas_in_group = sorted_df.loc[sorted_df["new_class"] == group, "aa"]
                 df_res.loc[idx, group + "_group"] = df.loc[idx, aas_in_group].sum()

         df_res.loc[:, ["T_group", "H_group", "M_group", "L_group", "B_group", "y"]]\
             .to_csv(str(output))