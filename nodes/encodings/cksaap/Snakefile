from modlamp.core import read_fasta
from iFeature import CKSAAP

import pandas as pd
import numpy as np
import os
import yaml

TOKEN = config["token"]
TARGET_FILES = config["csv_out"]

if type(TARGET_FILES) == list:
    TARGET_DIR = os.path.dirname(TARGET_FILES[0])
else:
    TARGET_DIR = os.path.dirname(TARGET_FILES)

rule all:
    input:
         config["csv_out"]

rule encode:
    input:
         config["fasta_in"],
         config["length_in"]
    output:
         temp(f"data/temp/{TOKEN}/cksaap_gap_{{gap_val}}.csv")
    run:
         seqs, names = read_fasta(str(input[0]))

         with open(str(input[1])) as f:
             max_gv = yaml.safe_load(f)

         fastas = [[n, s] for s, n in zip(seqs, names)]
         gv = int(wildcards.gap_val)

         if gv > max_gv:
             shell("touch {output}")
         else:
             res = np.array(CKSAAP.CKSAAP(fastas, gap=int(wildcards.gap_val), **{"order": None}))
             df = pd.DataFrame(res[1:, 1:], columns=res[0, 1:])
             df.index = np.array(res)[1:, 0]
             df.to_csv(str(output))

rule dump:
    input:
         f"data/temp/{TOKEN}/cksaap_gap_{{gap_val}}.csv",
         config["classes_in"]
    output:
         f"{TARGET_DIR}/cksaap_gap_{{gap_val}}.csv"
    run:
         with open(str(input[1])) as f:
             classes = list(map(lambda l: int(l.rstrip()), f.readlines()))

         if os.path.getsize(str(input[0])) == 0:
             shell("touch {output}")
         else:
             df = pd.read_csv(str(input[0]), index_col=0)
             df["y"] = classes
             df.to_csv(str(output))
