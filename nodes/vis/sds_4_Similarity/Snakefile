import altair as alt
import pandas as pd
import numpy as np

import joblib
import os

from nodes.vis.sds_4_Similarity.scripts.utils import cluster, path

TOKEN = config["token"]

SIMILARITY_DIR_IN = os.path.commonpath(
    [config["similarity_dir_group_1_in"], config["similarity_dir_group_2_in"]]) + "/"

rule all:
    input:
         config["html_dir_out"] + "sds_4_Similarity.json"

rule similarity_transform_data:
    input:
         SIMILARITY_DIR_IN + "{comparision}/{metric}.csv"
    output:
         config["html_dir_out"] + "{comparision}_{metric}_data.json"
    run:
         df = pd.read_csv(input[0], index_col=0)

         row_indices = cluster(df.values, axis=0)
         col_indices = cluster(df.values, axis=1)
         heatmap_data = df.iloc[row_indices, col_indices]

         x, y = np.meshgrid(range(0, heatmap_data.shape[1]), range(0, heatmap_data.shape[0]))
         source = pd.DataFrame({"x": x.ravel(), "y": y.ravel(), "Similarity": heatmap_data.values.ravel()})

         source["Encoding1"] = source["x"].apply(lambda i: heatmap_data.columns[i])
         source["Encoding2"] = source["y"].apply(lambda i: heatmap_data.index[i])

         source["Similarity_cat"] = source.Similarity.apply(
             lambda x: "0.0-0.1" if x <= 0.1 else "0.1-0.3" if x <= 0.3 else "0.3-0.6" if x <= 0.6 else "0.6-1.0")

         source.to_json(output[0], orient="records")

rule create_heatmap:
    input:
         config["html_dir_out"] + "{comparision}_{metric}_data.json"
    output:
         temp(f"data/temp/{TOKEN}/{{comparision}}_{{metric}}.hmjl")
    run:
         d, r = ["0.0-0.1", "0.1-0.3", "0.3-0.6", "0.6-1.0"], ["white", "gainsboro", "grey", "black"]

         x_config = alt.Axis(labels=False, ticks=False)
         y_config = alt.Axis(labels=False, ticks=False)

         comp, met = wildcards.comparision, wildcards.metric

         show_x_title, show_y_title = True, True
         if comp == "all_vs_all" and met == "diversity":
             show_x_title = True
         elif comp == "all_vs_all" and met == "phi":
             show_x_title, show_y_title = False, True
         elif comp == "seq_vs_str" and met == "phi":
             show_x_title, show_y_title = False, False
         else:
             show_x_title, show_y_title = True, False

         if not show_x_title:
             x_config = alt.Axis(labels=False, ticks=False, title=None)
         if not show_y_title:
             y_config = alt.Axis(labels=False, ticks=False, title=None)

         url = input[0]

         chart = alt.Chart(url).mark_rect().encode(
             x=alt.X('x:O', title="Encoding 2", axis=x_config),
             y=alt.Y('y:O', title="Encoding 1", axis=y_config),
             color=alt.Color(
                 "Similarity_cat:N",
                 scale=alt.Scale(domain=d, range=r),
                 legend=alt.Legend(title="Similarity")
             ),
             tooltip=["Encoding1:N", "Encoding2:N", "Similarity:Q"]
         ).properties(
             width=600,
             height=600
         )

         joblib.dump(chart, output[0])

rule create_similarity_chart:
    input:
         expand(f"data/temp/{TOKEN}/{{comparision}}_{{metric}}.hmjl",
                comparision=["all_vs_all", "seq_vs_str"],
                metric=["diversity", "phi"])
    output:
         config["html_dir_out"] + "sds_4_Similarity.json"
    run:
         paths = list(input)

         hm1 = joblib.load(path(paths, "all_vs_all", "phi"))
         hm2 = joblib.load(path(paths, "seq_vs_str", "phi"))
         hm3 = joblib.load(path(paths, "all_vs_all", "diversity"))
         hm4 = joblib.load(path(paths, "seq_vs_str", "diversity"))

         t1 = alt.hconcat(hm1, hm2, title=alt.TitleParams(text=["Phi correlation", ""]))
         t2 = alt.hconcat(hm3, hm4, title=alt.TitleParams(text=["Disagreement measure", ""]))

         chart_json = alt.vconcat(
             t1, t2,
             title=alt.TitleParams(
                 text=[
                     "Diversity of the classifier outputs between all encodings (left) and",
                     "sequence- vs. structure-based encodings (right),",
                     "based on correlation (top) and disagreement (bottom).",
                     "",
                     ""
                 ],
                 anchor="middle"
             ),
             config=alt.Config(
                 legend=alt.LegendConfig(titleFontSize=12, labelFontSize=12),
                 axis=alt.AxisConfig(titleFontSize=12, titleFontWeight="normal")
             )
         ).configure_view(strokeWidth=0).to_json(indent=None)

         with open(output[0], "w") as f:
             f.write(chart_json)
             f.flush()